* CHARMM 
*

prnlev 3 

!set the environment variables for TURBOMOLE

!qturboexe points to the "turbo.py" script that executes TURBOMOLE
envi qturboexe "< absolute path to /turbo.py>"
!"qturbooutpath" is the directory that includes "coord", "point_charges", and "gradient" files that are 
!copied back and forth between CHARMM and TURBOMOLE
envi qturbooutpath  "< absolute path to > /turbodir/"
!"qturboinpath" includes the input files,e.g. control, basis, that are required by TURBOMOLE
envi qturboinpath "< absolute path to > /data/"


stream ./charmm_input/toppar_qm.str

! generate QM region (one Mg(II) and 6 water molecules)

READ sequence MGQ 1
generate ION setup first none last none noangle nodihedral
READ sequence WATQ 6
generate IN setup first none last none  noangle nodihedral

! generate MM region (445 SWM4-NDP molecules)

READ sequence SWM4 445
generate OUT setup first none last none  noangle nodihedral DRUDE DMASS 0.4 HYPE HORD 4 KHYP 40000 RHYP 0.2 

! read the initial coordinaates

read coor card name ./charmm_input/mg_6h2o.crd

coor shake
coor sdrude


NBOND -
    atom   cdiel  eps 1.0 -               !Electrostatics
    vatom  vswitch        -               ! VdW
    elec   switch         -               ! Elec
    ctonnb 100.0 ctofnb 120.0  cutnb 160.0

faster off

! translate Mg(II) ion to origin and restrain 

coor stat select resname MGQ end
coor trans XDIR -?XAVE YDIR -?YAVE ZDIR -?ZAVE
cons harm force 5.0 select resname MGQ end


! define spherical boundary condition

MMFP
  GEO harmonic  spherical -
  FORCE 0.5 P1 2.25 -
  xref 0.0 yref 0.0 zref 0.0 -
  droff 14.5  select type OH2 end
END


!This tells CHARMM which molecules are in the QM region and
!then it deletes the MM internal energy terms for atoms
!in the QM region and flags these atoms to be represented using the QM model

define qm sele segid ION .or. segid IN end
qturbo remove sele qm end

!SHAKE settings for the bonds containing hydrogen and H-H bonds in water

shake bonh - 
    select .not. type D* end - 
    nofast

! set thermostat parameters: heavy atoms are coupled to 298.15 K thermostat,
!                            Drude particles coupled to 1.0 K thermostat

tpcontrol nther 3 nhgam 5.0 nhgamd 10.0 -
    ther 1 tref 298.15 lang sele (segid ION .or. segid IN) end -
    ther 2 tref 298.15 lang sele segid OUT .and. .not. type D* end -
    ther 3 tref 1.0 lang sele type D* end

!TPCONTROL NTHER 3  NSTEP 50  -
!        THER 1  TREF 298.15  TAU  0.1    SELECT segid ION .or. segid IN END  -
!        THER 2  TREF 298.15  TAU  0.1    SELECT .NOT. TYPE D* .and. segid OUT END  -
!        THER 3  TREF 1.0     TAU  0.001  SELECT TYPE D* .and. segid OUT END

open write unit 51 card name ./charmm_output/md.res
open write unit 52 card name ./charmm_output/md.dat
open write unit 53 file name ./charmm_output/md.dcd

! run 2500 steps of dynamics

DYNAMICS  vv2   start nstep   2500  timestep   0.001 -
    nprint 1 iprfrq   100 isvfrq 1 iasvel 1 -
    firstt 298.15 finalt 298.15 tstruc 298.15  -
    inbfrq -1   ilbfrq 0 iunread -1  nsavc 1 iuncrd 53 -
    iunwrite 51 kunit 52

stop
